#!/usr/bin/env dak

(import [:node:fs/promises [readFile writeFile]]
        [:node:path [join]]
        [:node:util [promisify]]
        [:node:child_process {exec execCB}]
        [:mri mri]
        [:semver/functions/inc.js semver-inc])

(const exec (promisify execCB))
(const PASS "\x1b[32mPASS\x1b[0m")
(const FAIL "\x1b[31m\x1b[1mFAIL\x1b[0m")

(fn since [start]
  (str (- (Date.now) start) "ms"))

(fn@ out [...args]
  (.trim (. @(exec ...args) :stdout)))

(fn@ all-workspaces []
  (-> @(readFile "package.json" :utf8) JSON.parse (. :workspaces)))

(fn@ workspace-package-json [ws]
  (-> (join ws "package.json") (readFile :utf8) await JSON.parse))

(fn@ run-ws [ws cmd ws-pad-target]
  (when-let [sh (-> @(workspace-package-json ws) (?. :scripts cmd))]
    (try
      (let [start (Date.now)
            {:keys [stdout stderr]} @(exec sh {:cwd ws})]
        (console.log PASS (ws.padEnd ws-pad-target " ")
          (.padStart (since start) 5 " ") sh)
        (when (not= cmd :test)
          (console.log stdout)
          (console.log stderr)))
      (catch e
        (console.log FAIL ws sh)
        (console.error e.stdout)
        (console.error e.stderr)
        false))))

(fn@ run [cmd]
  (let [workspaces @(all-workspaces)
        ws-pad-target (workspaces.reduce #(Math.max $1 $2.length) 0)]
    (if (.some @(Promise.allSettled (workspaces.map #(run-ws $ cmd ws-pad-target)))
               #(false? $.value))
      (process.exit 1))))

(fn r-dep-graph [pkgs]
  (let [graph {}]
    (pkgs.map #(set (. graph $.name) []))
    (pkgs.map
      (fn [pkg]
        (when-let [deps pkg.dependencies]
          (-> deps Object.keys
              (.map #(when (in $ graph)
                      (.push (. graph $) pkg.name)))))))
    graph))

(fn@ r-dep []
  (let [workspaces @(all-workspaces)
        pkgs @(Promise.all (workspaces.map workspace-package-json))]
    (console.log (r-dep-graph pkgs))))

(fn@ pkg-needs-bump [ws]
  (if-let [version-line @(out "grep -n '\"version\"' package.json|cut -d : -f 1"
                          {:cwd ws})]
    (let [last-version-rev @(out (str "git log -1 --quiet --format=%H -L "
                                      version-line "," version-line ":"
                                      (join ws "package.json")))]
      (not= "" @(out (str "git log -1 --format=%H "
                          last-version-rev "..main " ws))))
    false))

(fn ws-to-name [workspaces pkgs]
  (Object.fromEntries (workspaces.map #(do [$1 (. pkgs $2 :name)]))))

(fn name-to-index [pkgs]
  (Object.fromEntries (pkgs.map #(do [$1.name $2]))))

(fn@ release [...argv]
  (let [args (mri argv {:boolean [:dry-run] :alias {:dry-run :n}})
        release (?? (. args._ 0) :minor)
        workspaces @(all-workspaces)
        [needs-bump pkgs]
        @(Promise.all
           [(Promise.all (workspaces.map pkg-needs-bump))
            (Promise.all (workspaces.map workspace-package-json))])
        ws-names (ws-to-name workspaces pkgs)
        name-index (name-to-index pkgs)
        graph (r-dep-graph pkgs)
        all-bump (needs-bump.reduce
                   (fn [all needs index]
                     (when needs
                       (all.add (. ws-names (. workspaces index)))
                       (.map (. graph (. pkgs index :name)) #(all.add $)))
                     all)
                   (Set.))
        write-indexes (Set.)
        writes []]
    (for-of [name all-bump]
      (let [index (. name-index name)
            pkg (. pkgs index)
            new-version (semver-inc pkg.version release)]
        (console.log name pkg.version "=>" new-version)
        (when (not args.n)
          (set pkg.version new-version)
          (write-indexes.add index)
          (for-of [r-dep-name (. graph name)]
            (let [r-dep-index (. name-index r-dep-name)
                  r-dep-pkg (. pkgs r-dep-index)]
              (set (. r-dep-pkg.dependencies name) (str "^" new-version))
              (write-indexes.add r-dep-index))))))
    (when (pos? write-indexes.size)
      (let [ws-to-publish []]
        (for-of [index write-indexes]
          (let [ws (. workspaces index)]
            (ws-to-publish.push ws)
            (writes.push (writeFile (join ws "package.json")
                                    [(JSON.stringify (. pkgs index) null 2)
                                     "\n"]))))
        @(Promise.all writes)
        @(exec "git commit -a -m 'publishing releases'")
        @(exec "git push")
        @(exec (str "npm publish -w " (ws-to-publish.join " -w ")))))))

(const commands {:run run
                 :test #(run :test)
                 :cover #(run :cover)
                 :r-dep r-dep
                 :release release})

(fn@ main []
  (set process.env.PATH
       (str process.env.PATH ":" (join (process.cwd) "./node_modules/.bin")))
  (let [cmd-name (. process.argv 2)
        cmd (. commands cmd-name)]
    (when (not cmd)
      (console.error "unknown command:" cmd-name)
      (process.exit 1))
    (cmd (... (process.argv.slice 3)))))

@(main)
