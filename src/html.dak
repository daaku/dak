(const entities {"&" "&amp;"
                 "<" "&lt;"
                 ">" "&gt;"
                 "\"" "&#34;"
                 "'" "&#39;"})
(fn entities-replace [c] (. entities c))

(const self-closing
  (Set. [:area :base :br :col :command :embed :hr :img :input :keygen :link
         :meta :param :source :track :wbr]))

(fn escape [s]
  (s.replace #/[&<>"']/g entities-replace))

(fn str-val [v]
  (case (typeof v)
    :undefined ""
    :null ""
    :string (escape v)
    :number (String v)
    :boolean (String v)
    (escape (String v))))

(fn str-attrs [attrs]
  (let [s []]
    (for-of [[k v] (Object.entries attrs)]
      (s.push " " k "=\"" (str-val v) "\""))
    (s.join "")))

(fn attrs? [thing]
  (and thing (object? thing) (not (array? thing))))

(const tag-re #/(?<tag>[^#.]+)(?<id>#[^#.]+)?(?<klass>\.[^#]+)?/)

(fn klass-prep [k]
  (-> k (.slice 1) (.replace "." " ")))

(fn tag-attrs [given-tag attrs]
  (let [{:keys [tag id klass]} (. (tag-re.exec given-tag) :groups)]
    (when klass
      (set attrs.class
        (let [existing attrs.class]
          (if (array? existing) (str (klass-prep klass) " " (existing.join " "))
              (string? existing) (str (klass-prep klass) " " existing)
              (klass-prep klass)))))
    (when id
      (set attrs.id (id.slice 1)))
    [tag attrs]))

(fn end-tag [tag]
  (if (self-closing.has tag) "" (str "</" tag ">")))

(const _unsafe (Symbol :unsafe))
(fn ^:export unsafe [...vs]
  [_unsafe vs])

(fn ^:export html [el]
  (if (string? el) (escape el)
      (null? el) ""
      (undefined? el) ""
      (= (. el 0) _unsafe) (.join (. el 1) "")
      (let [given-tag (. el 0)
            second (. el 1)
            has-attrs (attrs? second)
            [tag attrs] (tag-attrs given-tag (if has-attrs second {}))
            body (el.slice (if has-attrs 2 1))]
        (str "<" tag (str-attrs attrs) ">"
             (.join (body.map html) "")
             (end-tag tag)))))

(fn ^:export doc [attrs ...els]
  (str "<!doctype html>"
       (html [:html attrs ...els])
       "\n"))
